apply plugin: 'maven'
apply plugin: 'signing'

// This is used so that relative version dependencies between the projects work.
// Don't remove this!
group = GROUP

def getReleaseRepositoryUrl(){
    return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL : ""
}

def getRepositoryUsername(){
    return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
}

def getRepositoryPassword(){
    return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
}

afterEvaluate{project ->
    uploadArchives{
        repositories{
            mavenDeployer{
                beforeDeployment{MavenDeployment deployment -> signing.signPom(deployment)}

                pom.groupId = GROUP
                pom.artifactId = POM_ARTIFACT_ID
                pom.version = VERSION_NAME

                repository(url: getReleaseRepositoryUrl()){
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }

                pom.project{
                    name POM_NAME
                    packaging POM_PACKAGING
                    description POM_DESCRIPTION
                    url POM_URL

                    developers{
                        developer{
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                        }
                    }
                }
            }
        }
    }

    signing{
        required{gradle.taskGraph.hasTask("uploadArchives")}
        sign configurations.archives
    }

    task install(type: Upload){
        repositories.mavenInstaller{
            configuration = configurations.archives

            pom.groupId = GROUP
            pom.artifactId = POM_ARTIFACT_ID
            pom.version = version

            pom.project{
                name POM_NAME
                packaging POM_PACKAGING
                description POM_DESCRIPTION
                url POM_URL

                developers{
                    developer{
                        id POM_DEVELOPER_ID
                        name POM_DEVELOPER_NAME
                    }
                }
            }
        }
    }
}